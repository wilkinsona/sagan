sourceSets {
    testUtil {
        java.srcDir 'src/test-util/java'
        resources.srcDir 'src/test-util/resources'
    }
}

dependencies {
    compile "org.springframework:spring-context"
    compile "org.springframework.security:spring-security-core"
    compile 'org.springframework.social:spring-social-github:1.0.0.M4'
    compile 'org.springframework:spring-webmvc' // Prevents Spring Social from pulling in 3.2.x
    compile "org.springframework.boot:spring-boot-starter-data-jpa"

    compile 'org.elasticsearch:elasticsearch:0.90.2'
    compile('io.searchbox:jest:0.0.4') {
        exclude(module: 'commons-logging')
    }
    compile 'org.springframework.cloud:spring-cloud-spring-service-connector'
    compile 'org.springframework.cloud:spring-cloud-cloudfoundry-connector'

    // datasource and connection pool dependencies
    runtime 'org.postgresql:postgresql:9.2-1002-jdbc4'
    compile 'com.h2database:h2'
    compile 'org.apache.tomcat:tomcat-jdbc'

    // for use in on-the-fly database setup and migrations
    compile 'org.flywaydb:flyway-core'

    compile('org.simpleframework:simple-xml:2.7') {
        exclude(module: 'stax')
        exclude(module: 'stax-api')
        exclude(module: 'xpp3')
    }

    compile 'org.jsoup:jsoup:1.7.2'
    compile 'org.yaml:snakeyaml'
    compile 'joda-time:joda-time'
    compile 'org.hibernate:hibernate-validator'

    // for use in rendering AsciiDoc-based guide content to HTML
    compile 'org.asciidoctor:asciidoctor-java-integration:0.1.4'

    testUtilCompile "junit:junit"
    testUtilCompile sourceSets.main.output
    testUtilCompile configurations.compile
    testUtilRuntime configurations.runtime

    // pick up common test utility classes
    testCompile sourceSets.testUtil.output

    // for use of spring-test's MatcherAssertionErrors.*
    testCompile "org.springframework:spring-test"
}

// Add test-util source directories to IDEA .iml files
idea.module {
    testSourceDirs += (sourceSets.testUtil.java.srcDirs + sourceSets.testUtil.resources.srcDirs)
}

// sagan-common is not technically a "Spring Boot" application--it is just a library.
// However, we apply the spring-boot plugin here anyway in order to pick up boot's
// default dependency versioning, such that we stay in sync with other modules that
// are actually "boot"-style apps.
apply plugin: 'spring-boot'
bootRepackage.enabled = false

dependencies {
    versionManagement 'io.spring.platform:platform-versions:1.0.0.RC1@properties'
}